#Fits, characterizes, and plots the impedance of a loudspeaker
cd 'C:\Users\Grant\Desktop\SkidLess\Testing Results\SoilMoistureTests_V2'
set xrange[*:*]
set yrange[*:*]
unset y2tics

# SOIL MOISTURE MODEL
model_SM(x, m, alpha) = m*(x**alpha)

# FITTING
# Fitting Model to Sample 1
m1 = 3
alpha1 = -2
fit log(model_SM(x, m1, alpha1)) 'SoilMoisture_Sample1.txt' \
	u 1:(log($3)) via m1, alpha1

# Fitting Model to Sample 2
m2 = m1
alpha2 = alpha1
fit log(model_SM(x, m2, alpha2)) 'SoilMoisture_Sample2.txt' \
	u 1:(log($3)) via m2, alpha2

# Fitting Model to Sample 3
m3 = m2
alpha3 = alpha2
fit log(model_SM(x, m3, alpha3)) 'SoilMoisture_Sample3.txt' \
	u 1:(log($3)) via m3, alpha3
	
set key top right Left
set grid
unset style
set logscale xy
set xrange[2:300]
set yrange[*:*]
set format x '%.0s%cg'
set format y '%.0s%cOhms'
set title 'Fitting Resistive Soil Moisture Samples to Affine Models' font 'Verdana, 20'
set ylabel 'Inter-Probe Resistance (ohms)' font 'Verdana, 18'
set xlabel 'Gravimetric Water Content (g)' font 'Verdana, 16'

plot model_SM(x, m1, alpha1) lw 2 lc 'brown' \
		title sprintf('Sample 1:   %.0f x^{%.2f}', m1, alpha1), \
	model_SM(x, m2, alpha2) lw 2 lc 'navy' \
		title sprintf('Sample 2:    %.0f x^{%.2f}', m2, alpha2), \
	model_SM(x, m3, alpha3) lw 2 lc 'red' \
		title sprintf('Sample 3:    %.0f x^{%.2f}', m3, alpha3), \
	'SoilMoisture_Sample1.txt' u 1:3 pt 12 lw 3 lc 'brown' notitle, \
	'SoilMoisture_Sample2.txt' u 1:3 pt 06 lw 3 lc 'navy' notitle, \
	'SoilMoisture_Sample3.txt' u 1:3 pt 14 lw 3 lc 'red' notitle